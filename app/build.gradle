apply plugin: 'com.android.application'
apply plugin: "org.sonarqube"

sonarqube {
    properties {
        property "sonar.projectName", "HOFC Android"
        property "sonar.version", "1.0"

        property "sonar.sources", "./src/main/java"
        property "sonar.tests", "./src/androidTest/java"

        property "sonar.java.binaries", "./build/intermediates/classes/prod/release"
        property "sonar.junit.reportsPath", "./build/test-results/release"

        property "sonar.android.lint.report", "./build/outputs/lint-results.xml"

        property "sonar.language", "java"
        property "sonar.verbose", "false"
        property "sonar.scm.provider", "git"
    }
}
android {
    compileSdkVersion 23
    buildToolsVersion '23.0.1'
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "com.hofc.hofc"
        minSdkVersion 14
        targetSdkVersion 21
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // Enabling multidex support.
        multiDexEnabled true
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            proguardFiles 'proguard-rules.txt'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    productFlavors {
        development {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("development.properties")))
            buildConfigField "String", "SENDER_ID",props.senderid
            buildConfigField "String", "SERVER_URL_PREFIX", "\"http://\""
            buildConfigField "String", "SERVER_URL", "\"hofcdev.azurewebsites.net/api\""
            buildConfigField "int", "SERVER_PORT", "0"
        }
        prod {
            def props = new Properties()
            props.load(new FileInputStream(rootProject.file("release.properties")))
            buildConfigField "String", "SENDER_ID",props.senderid
            buildConfigField "String", "SERVER_URL_PREFIX", "\"https://\""
            buildConfigField "String", "SERVER_URL", "\"quiet-wave-7010.herokuapp.com\""
            buildConfigField "int", "SERVER_PORT", "0"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'LICENSE.txt'
    }
}

dependencies {
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.google.android.gms:play-services:8.1.0'
    compile 'com.mcxiaoke.volley:library:1.0.17'
    compile 'de.hdodenhof:circleimageview:1.3.0'
    compile ('com.github.afollestad.material-dialogs:core:0.8.5.3@aar') {
        transitive = true
    }
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile 'com.fasterxml.jackson.core:jackson-core:2.5.+'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.5.+'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.5.+'
    androidTestCompile 'com.android.support:support-annotations:23.1.1'
    provided 'com.squareup.okhttp:mockwebserver:2.4.0'
    androidTestCompile 'com.squareup.okhttp:mockwebserver:2.4.0'

    // Testing-only dependencies
    androidTestCompile 'com.android.support.test:runner:0.3'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2'
    androidTestCompile ('com.android.support.test.espresso:espresso-contrib:2.2'){
        exclude group: 'com.android.support', module: 'appcompat'
        exclude group: 'com.android.support', module: 'support-v4'
        exclude module: 'recyclerview-v7'
    }
}
